//
//  Copyright (c) 2011-2014, ARM Limited. All rights reserved.
//
//  SPDX-License-Identifier: BSD-2-Clause-Patent
//
//

    AREA |.text|, CODE, READONLY
    ALIGN 3

    EXPORT _ModuleEntryPoint

_ModuleEntryPoint PROC
    // Do early platform specific actions
    bl      ArmPlatformPeiBootAction

// NOTE: We could be booting from EL3, EL2 or EL1. Need to correctly detect
//       and configure the system accordingly. EL2 is default if possible.
// If we started in EL3 we need to switch and run at EL2.
// If we are running at EL2 stay in EL2
// If we are starting at EL1 stay in EL1.

// If started at EL3 Sec is run and switches to EL2 before jumping to PEI.
// If started at EL1 or EL2 Sec jumps directly to PEI without making any
// changes.

// Which EL are we running at? Every EL needs some level of setup...
// We should not run this code in EL3
    EL1_OR_EL2(x0)
1:  bl      SetupExceptionLevel1
    b       MainEntryPoint
2:  bl      SetupExceptionLevel2
    b       MainEntryPoint

MainEntryPoint:
    // Get the top of the primary stacks (and the base of the secondary stacks)
    ldr     x1, =FixedPcdGet64(PcdCPUCoresStackBase) + FixedPcdGet32(PcdCPUCorePrimaryStackSize)

    // Set up the stack pointer
    mov     sp, x1

    // Apply the init value to the entire stack
    ldr     x8, =FixedPcdGet64(PcdCPUCoresStackBase)
    ldr     x9, =FixedPcdGet32(PcdInitValueInTempStack) | FixedPcdGet32(PcdInitValueInTempStack) << 32
0:  stp     x9, x9, [x8], #16
    cmp     x8, x1
    b.lt    0b

    // The PEI Core Entry Point has been computed by GenFV and stored in the second entry of the Reset Vector
    ldr     x2, =FixedPcdGet64(PcdFvBaseAddress)

    // Move sec startup address into a data register
    // Ensure we're jumping to FV version of the code (not boot remapped alias)
    ldr   x3, =CEntryPoint

    // Set the frame pointer to NULL so any backtraces terminate here
    mov   x29, xzr

    // Jump to PrePeiCore C code
    //    x0 = pei_core_address
    blr   x3

    ret
    ENDP

    END